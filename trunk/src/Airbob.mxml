<?xml version="1.0" encoding="utf-8"?>
<mx:WindowedApplication 
	xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns:control="be.nascom.airbob.control.*" 
	xmlns:view="be.nascom.airbob.view.*" 
	xmlns:business="be.nascom.airbob.business.*"	
	layout="absolute" 
	closeEffect="fadeOut"
	showEffect="fadeIn"
	showGripper="true"
	showTitleBar="true"
	showStatusBar="false"
	backgroundColor="White"
	dropShadowEnabled="true" 
	creationComplete="init()">
	
	<control:AppController id="controller" />
	<business:Services id="services"/>
	
	<mx:Script>
		<![CDATA[
			import be.nascom.airbob.events.LoadConfigEvent;
			import mx.logging.Log;
			import mx.logging.ILogger;
			import be.nascom.airbob.vo.ServerConfig;
			import be.nascom.airbob.events.LoadProjectsEvent;
			import be.nascom.airbob.commands.LoadProjectsCommand;
			import com.adobe.cairngorm.model.ModelLocator;
			import be.nascom.airbob.model.AppModelLocator;
			
			private static var logger:ILogger = Log.getLogger("Airbob");
			
			[Bindable]
			private var model:AppModelLocator = AppModelLocator.getInstance();	
			
			/**
			 * Init the app
			 */
			private function init():void {	
				new LoadConfigEvent().dispatch();
				
				// Add the event listsners for systray
		    	addEventListener(Event.ADDED, prepareForSystray);
		    	addEventListener(Event.CLOSING, closingApplication);			    			    				    			    	
		    			    		    			    			    
		  	}
		  	
		  	public function prepareForSystray(event:Event):void {											
				if (NativeApplication.supportsSystemTrayIcon){
					setSystemTrayProperties();
					SystemTrayIcon(NativeApplication.nativeApplication.icon).menu = createSystrayRootMenu();	
					NativeApplication.nativeApplication.icon.bitmaps = [model.trayIcon];				
				}													
			}	
			
			private function createSystrayRootMenu():NativeMenu {
				var menu:NativeMenu = new NativeMenu();
				var openNativeMenuItem:NativeMenuItem = new NativeMenuItem("Dashboard");
				var configNativeMenuItem:NativeMenuItem = new NativeMenuItem("Config");
				var exitNativeMenuItem:NativeMenuItem = new NativeMenuItem("Close");
				
				openNativeMenuItem.addEventListener(Event.SELECT, undock);
				configNativeMenuItem.addEventListener(Event.SELECT, openConfig);
				exitNativeMenuItem.addEventListener(Event.SELECT, closeApp);
				
				menu.addItem(openNativeMenuItem);
				menu.addItem(configNativeMenuItem);
				menu.addItem(new NativeMenuItem("",true));
				menu.addItem(exitNativeMenuItem);
				
				return menu;
			}
			
			private function setSystemTrayProperties():void {
				SystemTrayIcon(NativeApplication.nativeApplication.icon).tooltip = "Airbob";
				SystemTrayIcon(NativeApplication.nativeApplication.icon).addEventListener(MouseEvent.CLICK, undock);				    	     		
			}
			
			private function nwMinimized(displayStateEvent:NativeWindowDisplayStateEvent):void {
				if(displayStateEvent.afterDisplayState == NativeWindowDisplayState.MINIMIZED) {
					displayStateEvent.preventDefault();
					dock();
				} 
			}
			
			public function dock():void  {
				stage.nativeWindow.visible = false;
				NativeApplication.nativeApplication.icon.bitmaps = [model.trayIcon];
			}	
			
			public function openConfig(event:Event):void {
				stage.nativeWindow.visible = true;	
				stage.nativeWindow.orderToFront();
				
				model.selectedView = AppModelLocator.VIEW_PREFERENCES;
			}
			
			/**
			 * Show the application again and remove the application icon from the systray
			 */
			public function undock(event:Event):void {
				stage.nativeWindow.visible = true;	
				stage.nativeWindow.orderToFront();
				
				model.selectedView = AppModelLocator.VIEW_DASHBOARD;
			}
			
			private function closingApplication(event:Event):void {	
				logger.debug("Closing application");			
				event.preventDefault();
				dock();		
			}				
			
			private function closeApp(event:Event):void {
				stage.nativeWindow.close();
			}			
		]]>
	</mx:Script>
	
	<!--mx:Style source="../assets/skin/airbob_skins.css" />-->
	
	<mx:Style >
		@font-face {
		 	src:url("../assets/skin/airbob_fonts.swf"); 
		 	fontFamily: "EN Moist";
		 	fontWeight:normal;
		}	
		@font-face {
		 	src:url("../assets/skin/airbob_fonts.swf"); 
		 	fontFamily: "Helvetica 35 Thin";
		} 			
		@font-face {
		 	src:url("../assets/skin/airbob_fonts.swf"); 
		 	fontFamily: "Helvetica 65 Medium";
		} 
		@font-face {
		 	src:url("../assets/skin/airbob_fonts.swf"); 
		 	fontFamily: "Helvetica 65 Medium";
		 	fontWeight:bold;
		} 			
		
	</mx:Style>	
	
	<mx:ViewStack id="viewStack" selectedIndex="{model.selectedView}" width="100%" height="100%">
	
		<view:DashboardView id="dashboardView" width="100%" height="100%"/>
		<view:ConfigView id="preferencesView" width="100%" height="100%"/>
	
	</mx:ViewStack>
	
	<mx:Canvas width="100%" height="100%">
		<mx:Image source="@Embed('../assets/images/nascom_logo.png')" left="10" bottom="10"/>					
	</mx:Canvas>		
	
</mx:WindowedApplication>
